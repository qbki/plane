function (append_license_title result_string lib_name version)
  string(APPEND ${result_string} "***** ${lib_name} ${version} *****\n\n")
  return(PROPAGATE ${result_string})
endfunction()

function(append_license_file result_string lib_path)
  set(license_file_pathes "")
  file(GLOB license_file_pathes LIST_DIRECTORIES false "${lib_path}/licenses/*")
  foreach(file_path ${license_file_pathes})
    set(file_content "")
    file(READ ${file_path} file_content)
    string(APPEND ${result_string} "${file_content}\n")
  endforeach()
  return(PROPAGATE ${result_string})
endfunction()

function(append_license result_string lib_name version_prefix folder_prefix)
  set(version ${${version_prefix}_VERSION_STRING})
  set(lib_path ${${folder_prefix}_PACKAGE_FOLDER_RELEASE})
  append_license_title(${result_string} ${lib_name} ${version})
  append_license_file(${result_string} ${lib_path})
  return(PROPAGATE ${result_string})
endfunction()
